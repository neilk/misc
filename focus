#!/usr/bin/env perl -w

#
# This program is meant to help me focus, by blocking timewasting sites in /etc/hosts.
#
# If the program is invoked with 'focus on', a section will be added to the /etc/hosts file
# routing requests for the domains in __DATA__ to localhost.
#
# If the program is invoked with 'focus off', that section will be removed, but it will first
# wait for 60 seconds.  So there is no instant gratification if you just *have* to post to 
# Facebook or whatever. You can press any key to turn focus back on. After 60 minutes, focus 
# will automatically go back to on.
#
# The section in /etc/hosts should be apparently updated in place. It should begin with the line "# FOCUS" 
# and, optionally, end with the line "# ENDFOCUS"
#

use strict;

use File::Temp qw/tempfile/;
use POSIX qw/strftime/;
use Term::ReadKey;

my $INITIAL_DELAY = 60;
my $MAX_DISTRACTION_DURATION = 60 * 60;  # 1 hour
my $MAX_DAILY_DISTRACTION_DURATION = 2 * 60 * 60;  # 2 hours
my $MAX_DAILY_DISTRACTIONS = 4;
my $DEFAULT_RECOMMIT_DELAY = 5 * 60 * 60;

my $distractionRecordFile = "$ENV{'HOME'}/.focus";

my $hostFileName = "/etc/hosts";

sub usage {
  die "Usage: $0 [on|off|offquick|movie|hangout|comms]\n";
}

# note, sometimes @$domains = []
sub printFocusSection {
  my ($outFh, $domainSpecs) = @_;
  print $outFh "# FOCUS\n";
  for my $domainSpec (@$domainSpecs) {
    for my $subDomain ('', 'www.') {
      my $domain = $subDomain . $domainSpec;
      # need to specify it twice; on MacOS X, will wait for the IPv6 lookup to time out
      for my $ip ('127.0.0.1', 'fe80::1%lo0') {
        print $outFh "$ip $domain\n";
      }
    }
  }
  print $outFh "# ENDFOCUS\n";
}

sub getDomainSpecs {
  my $excludeRegex = shift;
  my @domainSpecs;
  my $dataPos = tell DATA; # saved position
  while (<DATA>) {
    if ($excludeRegex) {
      next if /$excludeRegex/;
    }
    chomp;
    push @domainSpecs, $_;
  }
  seek DATA, $dataPos, 0; # seek to the beginning so we can read it again
  return \@domainSpecs;
}

sub rewriteHostsFile {
  my $excludeRegex = shift;

  my $domainSpecs = getDomainSpecs($excludeRegex);

  my ($outFh, $outFileName) = tempfile();

  open my $inFh, "<", $hostFileName or die $!;
  my $isReadingFocusSection = 0;
  my $isFocusSectionPrinted = 0;
  while (my $line = <$inFh>) {
    if ($line =~ /^# FOCUS/) {
      printFocusSection($outFh, $domainSpecs);
      $isFocusSectionPrinted = 1;
      $isReadingFocusSection = 1;
      next;
    } 
    if ($line =~ /^# ENDFOCUS/) {
      $isReadingFocusSection = 0;
      next;
    }
    if (!$isReadingFocusSection) {
      print $outFh $line;
    }
  }

  if (!$isFocusSectionPrinted) {
    printFocusSection($outFh, $domainSpecs);
  }

  chmod 0644, $outFileName or die $!;
  rename $outFileName, $hostFileName or die $!;

}

sub end() {
  ReadMode 0; # reset tty before exiting
  alarm 0; # reset alarm
  exit();
}

sub today {
  return strftime "%Y-%m-%d", localtime;
}

sub getTodayDistractions {
  my $today = today();
  my $count = 0;
  my $totalDuration = 0;
  if (-e $distractionRecordFile) {
    open my $fh, "<", $distractionRecordFile or die $!;
    while (<$fh>) {
      my ($date, $duration) = split;
      if ($date eq $today) {
        $count++;
        $totalDuration += $duration;
      }
    }
  }
  return ($count, $totalDuration);
}

sub recordDistraction {
  my ($duration) = @_;
  open my $fh, ">>", $distractionRecordFile or die $!;
  print {$fh} today() . "\t" . $duration . "\n";
}

sub configure {
  my $type = shift;
  my $duration = $MAX_DISTRACTION_DURATION;
  my $initialDelay = $INITIAL_DELAY;
  my $recommitDelay = $DEFAULT_RECOMMIT_DELAY;
  if ($type eq "movie") {
    $duration = 60 * 60 * 4;
  } elsif ($type eq "offquick") {
    $initialDelay = 0;
    $duration = 120;
  } elsif ($type eq "hangout") {
    $initialDelay = 0;
    $duration = 60 * 60 * 4;
  } elsif ($type eq "comms") {
    $duration = 60 * 60 * 4;
    $recommitDelay = 60 * 15;
  }
  return ($duration, $initialDelay, $recommitDelay);
}

#
# stop from doing it too much
#
sub checkIfTooOften {
  my ($type, $duration) = @_;
  if ($type ne "hangout" and $type ne "comms") {
    my ($todayDistractions, $todayDistractionDuration) = getTodayDistractions();
    if ($duration + $todayDistractionDuration > $MAX_DAILY_DISTRACTION_DURATION) {
      warn "Too much distraction today. Do something off the computer, or something beneficial.\n";
      exit(0);
    }
    if (1 + $todayDistractions > $MAX_DAILY_DISTRACTIONS) {
      warn "You have done that too much today. Do something off the computer, or something beneficial.\n";
      exit(0);
    }
  }
}

sub record {
  my ($type, $duration) = @_;
  if ($type ne "hangout" and $type ne "comms") {
    recordDistraction($duration);
  }
}

sub block {
  warn "Focus on again.\n"; 
  rewriteHostsFile(undef);
};

sub blockAndEnd {
  block();
  end(); 
};

# this should be an object now
sub unblockLoop {
  my ($type, $domain, $duration, $initialDelay, $recommitDelay) = @_;
  ## Actually do it
  warn "sleeping for $initialDelay seconds...\n";
  sleep($initialDelay); 

  my $excludeRegex;
  if ($domain) {
    $excludeRegex = $domain;  # exclude only this domain, which means allow these domains
  } elsif ($type eq 'hangout') {
    $excludeRegex = 'youtube';
  } else {
    $excludeRegex = '.'; # match anything, therefore exclude everything
  }

  my $unblock = sub {
    warn "Focus off.\n"; 
    rewriteHostsFile($excludeRegex);
  };

  my $recommit = sub {
    print "Are you still doing what you intended? (y/N)\n";

    block();

    my $key;
    while (not defined ($key = ReadKey(-1))) {
      sleep(1);
    }
    if ($key !~ /^y/i) {
      blockAndEnd();
    }
      
    $unblock->();
  };


  my $message = sprintf(
    "Press any key to block distraction sites again. Will automatically reblock after %d minutes.",
    int($duration / 60)
  );
  if ($recommitDelay < $duration) {
    $message .= sprintf(" Will ask to reconfirm every %d minutes.", int($recommitDelay / 60));
  }
  $message .= "\n";
  warn $message;

  $unblock->();

  local $SIG{'ALRM'} = \&blockAndEnd;
  alarm($duration); 

  ReadMode 4; # turn off control keys
  my ($startTime, $recommitTime);
  $startTime = $recommitTime = time();
  while (not defined (ReadKey(-1))) {
    sleep(1);
    if (time() > $recommitTime + $recommitDelay) {
      $recommit->();
      $recommitTime = time();
    }
  }

  blockAndEnd();
}


if (@ARGV < 1) {
  usage();
}


my $type = $ARGV[0];
my $domain = $ARGV[1];
if ($type eq "on") {
  rewriteHostsFile(undef);
  # do nothing. By default, will create entries for all domains, with no exclusions
} elsif ($type eq "off" 
          or $type eq "movie" 
          or $type eq "offquick" 
          or $type eq "hangout" 
          or $type eq "comms") {
  my ($duration, $initialDelay, $recommitDelay) = configure($type);
  checkIfTooOften($type, $duration);
  record($type, $duration);
  unblockLoop($type, $domain, $duration, $initialDelay, $recommitDelay);
} else {
  usage();
}

exit 0;


__DATA__
9gag.com
cracked.com
digg.com
ello.co
facebook.com
hulu.com
imgur.com
lifehacker.com
loseit.com
medium.com
netflix.com
news.ycombinator.com
nytimes.com
okcupid.com
overcomingbias.com
quora.com
reddit.com
techcrunch.com
theonion.com
tvtropes.com
twitter.com
wired.com
xkcd.com
ycombinator.com
youtube.com

#!/usr/bin/env perl -w

#
# This program is meant to help me focus, by blocking timewasting sites in /etc/hosts.
#
# If the program is invoked with 'focus on', a section will be added to the /etc/hosts file
# routing requests for the domains in __DATA__ to localhost.
#
# If the program is invoked with 'focus off', that section will be removed, but it will first
# wait for 60 seconds.  So there is no instant gratification if you just *have* to post to 
# Facebook or whatever.
#
# The section in /etc/hosts should be apparently updated in place. It should begin with the line "# FOCUS" 
# and, optionally, end with the line "# ENDFOCUS"
#

use strict;

use File::Temp qw/tempfile/;

my $hostFileName = "/etc/hosts";

sub usage {
  die "Usage: $0 [on|off]\n";
}

# note, sometimes @$domains = []
sub printFocusSection {
  my ($outFh, $domainSpecs) = @_;
  print $outFh "# FOCUS\n";
  for my $domainSpec (@$domainSpecs) {
    for my $subDomain ('', 'www.') {
      my $domain = $subDomain . $domainSpec;
      # need to specify it twice; on MacOS X, will wait for the IPv6 lookup to time out
      for my $ip ('127.0.0.1', 'fe80::1%lo0') {
        print $outFh "$ip $domain\n";
      }
    }
  }
  print $outFh "# ENDFOCUS\n";
}

sub getDomainSpecs {
  my $excludeRegex = shift;
  my @domainSpecs;
  while (<DATA>) {
    if ($excludeRegex) {
      next if /$excludeRegex/;
    }
    chomp;
    push @domainSpecs, $_;
  }
  return \@domainSpecs;
}


if (@ARGV < 1) {
  usage();
}

my $excludeRegex;

if ($ARGV[0] eq "on") {
  # do nothing. By default, will create entries for all domains, with no exclusions
} elsif ($ARGV[0] eq "off") {
  if ($ARGV[1]) {
    $excludeRegex = $ARGV[1];  # exclude only this domain, which means allow these domains
  } else {
    $excludeRegex = '.'; # match anything, therefore exclude everything
  }
} else {
  usage();
}
  
my $domainSpecs = getDomainSpecs($excludeRegex);

my ($outFh, $outFileName) = tempfile();

open my $inFh, "<", $hostFileName or die $!;
my $isReadingFocusSection = 0;
my $isFocusSectionPrinted = 0;
while (my $line = <$inFh>) {
  if ($line =~ /^# FOCUS/) {
    printFocusSection($outFh, $domainSpecs);
    $isFocusSectionPrinted = 1;
    $isReadingFocusSection = 1;
    next;
  } 
  if ($line =~ /^# ENDFOCUS/) {
    $isReadingFocusSection = 0;
    next;
  }
  if (!$isReadingFocusSection) {
    print $outFh $line;
  }
}

if (!$isFocusSectionPrinted) {
  printFocusSection($outFh, $domainSpecs);
}


chmod 0644, $outFileName or die $!;
rename $outFileName, $hostFileName or die $!;

__DATA__
9gag.com
cracked.com
digg.com
facebook.com
hulu.com
imgur.com
instagram.com
lifehacker.com
loseit.com
netflix.com
news.ycombinator.com
nytimes.com
okcupid.com
overcomingbias.com
quora.com
reddit.com
techcrunch.com
theonion.com
tvtropes.com
twitter.com
wired.com
xkcd.com
ycombinator.com
youtube.com

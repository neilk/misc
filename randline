#!/usr/bin/env perl -w

use strict;

my ($n, $file) = @ARGV;
unless (defined $n and $n =~ /^\d+$/ and $n != 0) {
  die <<EOUSAGE
Sample usage: $0 10 /usr/share/dict/words
Omit file to read from STDIN
Send SIGUSR1 for in-progress results
EOUSAGE
}
my $fh = *STDIN;
if (defined $file) {
  open $fh, '<', $file or die "Can't open $file -- $!";
}
$SIG{'USR1'} = \&showResults;

my @line;

while (<$fh>) {
  if ((int rand $.) <= $n) {
    if (@line < $n) {
      splice(@line, int rand @line, 0, $_);
    } else {
      $line[rand $n] = $_;
    }
  }
}

showResults();

sub showResults {
  if ($n > $.) {
    warn "not enough lines in input; wanted $n, got $.\n";
  }
  for my $s (@line) {
    print $s;
  } 
}

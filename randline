#!/usr/bin/env perl -w

use strict;

my ($n, $file) = @ARGV;
unless (defined $n and $n =~ /^\d+$/) {
  die <<EOUSAGE
Sample usage: $0 10 /usr/share/dict/words
Omit file to read from STDIN
Send SIGUSR1 for in-progress results
EOUSAGE
}
my $fh = *STDIN;
if (defined $file) {
  open $fh, '<', $file or die "Can't open $file -- $!";
}
$SIG{'USR1'} = \&showResults;

my @line;

while (<$fh>) {
  if ((int rand $.) <= $n) {
    if (@line < $n) {
      push @line, $_;
    } else {
      $line[rand $n] = $_;
    }
  }
}

showResults();

sub showResults { 
  for my $s (@line) {
    print $s;
  } 
}
